apply plugin: 'com.android.application'
apply plugin: 'jacoco'
apply plugin: 'com.mob.sdk'

MobSDK {
    appKey "28765cffe4a40"
    appSecret "5f3c3c3c32e727d3fb2c874f7446150a"
    ShareSDK {
        devInfo {
            Wechat {
                appId "wxec99b1ef4e23b81e"
                appSecret "38a4f8204cc784f81f9f0daaf31e02e3"
                bypassApproval false
//                userName "gh_afb25ac019c9"
//                path "pages/index/index.html?id=1"
//                withShareTicket true
//                miniprogramType 2
            }
            WechatMoments {
                appId "wxec99b1ef4e23b81e"
                appSecret "38a4f8204cc784f81f9f0daaf31e02e3"
                bypassApproval false
            }
        }
    }
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.se.npe.androidnote"
        minSdkVersion 25
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        // Enable the coverage report for unit test
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
        unitTests {
            includeAndroidResources = true
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ["libs"]
        }
    }
}

repositories {
    jcenter()
}

dependencies {
    configurations {
        all*.exclude group: 'com.android.support', module: 'support-v13'
    }
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:cardview-v7:28.0.0'

    // JUnit 4 framework
    testImplementation 'junit:junit:4.12'

    // Mockito framework
    testImplementation 'org.mockito:mockito-core:1.10.19'

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'

    // Espresso dependencies
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-contrib:3.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-intents:3.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-accessibility:3.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-web:3.0.2'
    androidTestImplementation 'com.android.support.test.espresso.idling:idling-concurrent:3.0.2'

    // Robolectric dependencies
    testImplementation "org.robolectric:robolectric:3.8"

    implementation 'org.greenrobot:eventbus:3.0.0'
    // View dependencies
    implementation 'com.marshalchen.ultimaterecyclerview:library:0.7.3'
    implementation project(path: ':lib:SlidingMenu')
    configurations.all {
        exclude group: 'com.android.support', module: 'support-v13'
    }
    implementation 'com.getbase:floatingactionbutton:1.10.1'
    implementation 'com.dmcBig:mediapicker:3.0'
    implementation 'cn.jzvd:jiaozivideoplayer:6.3.1'
    implementation 'com.googlecode.mp4parser:isoparser:1.1.21'
    implementation files('libs/Msc.jar')
    implementation files('libs/Sunflower.jar')
    // Pdf dependencies
    implementation 'com.itextpdf:itextg:5.5.10'
    implementation 'com.yydcdut:markdown-processor:0.1.3'
}

task jacocoTestReport(type: JacocoReport) {
    // " dependsOn: 'testDebugUnitTest' " property is ignored because test has been performed
    group = "Reporting"

    reports {
        xml.enabled = true
        html.enabled = false
    }

    def fileFilter = ['**/R.class',
                      '**/R$*.class',
                      '**/BuildConfig.*',
                      '**/Manifest*.*',
                      '**/*Test*.*',
                      'android/**/*.*']
    def debugTree = fileTree(dir: "${project.buildDir}/intermediates/javac/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = files("${project.buildDir}/jacoco/testDebugUnitTest.exec")
}