apply plugin: 'com.android.application'
apply plugin: 'jacoco'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.se.npe.androidnote"
        minSdkVersion 25
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        // Enable the coverage report for unit test
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
        unitTests {
            includeAndroidResources = true
        }
    }
    packagingOptions {
        // ITextPdf font configurations
        exclude 'com/itextpdf/text/pdf/fonts/cmap_info.txt'
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ["libs"]
        }
    }
}

repositories {
    jcenter()
}

dependencies {
    configurations {
        all*.exclude group: 'com.android.support', module: 'support-v13'
    }

    implementation 'co.lujun:androidtagview:1.1.4'

    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:cardview-v7:28.0.0'
    // JUnit 4 framework
    testImplementation 'junit:junit:4.12'
    // Mockito framework
    testImplementation 'org.mockito:mockito-core:1.10.19'
    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    // Espresso dependencies
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-contrib:3.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-intents:3.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-accessibility:3.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-web:3.0.2'
    androidTestImplementation 'com.android.support.test.espresso.idling:idling-concurrent:3.0.2'
    // Robolectric dependencies
    testImplementation 'org.robolectric:robolectric:4.0.1'
    implementation 'org.greenrobot:eventbus:3.0.0'
    // View dependencies
    implementation 'com.marshalchen.ultimaterecyclerview:library:0.7.3'
    configurations.all {
        exclude group: 'com.android.support', module: 'support-v13'
    }
    implementation 'com.getbase:floatingactionbutton:1.10.1'
    // Media dependencies
    implementation 'com.dmcBig:mediapicker:3.0'
    implementation 'cn.jzvd:jiaozivideoplayer:6.3.1'
    implementation 'com.googlecode.mp4parser:isoparser:1.1.21'
    // Ifly for audio to text dependencies
    implementation files('libs/Msc.jar')
    implementation files('libs/Sunflower.jar')
    // Pdf dependencies
    implementation 'com.itextpdf:itextg:5.5.10'
    implementation 'com.itextpdf:itext-asian:5.2.0'
    // MarkDown dependencies
    implementation 'com.yydcdut:markdown-processor:0.1.3'
    // frontend of tag in editor
    implementation 'com.github.donkingliang:LabelsView:1.4.5'
    implementation 'com.yydcdut:rxmarkdown-wrapper:0.1.3'
    // MobSdk for share dependencies
    implementation files('libs/mob_sdk/MobCommons-2018.1011.1129.jar')
    implementation files('libs/mob_sdk/MobTools-2018.1009.1056.jar')
    implementation files('libs/mob_sdk/ShareSDK-Core-3.3.0.jar')
    implementation files('libs/mob_sdk/ShareSDK-QQ-3.3.0.jar')
    implementation files('libs/mob_sdk/ShareSDK-QZone-3.3.0.jar')
    implementation files('libs/mob_sdk/ShareSDK-SinaWeibo-3.3.0.jar')
    implementation files('libs/mob_sdk/ShareSDK-Wechat-3.3.0.jar')
    implementation files('libs/mob_sdk/ShareSDK-Wechat-Core-3.3.0.jar')
    implementation files('libs/mob_sdk/ShareSDK-Wechat-Favorite-3.3.0.jar')
    implementation files('libs/mob_sdk/ShareSDK-Wechat-Moments-3.3.0.jar')
    implementation project(':aFileChooser')
}

task jacocoTestReport(type: JacocoReport) {
    // " dependsOn: 'testDebugUnitTest' " property is ignored because test has been performed
    group = "Reporting"

    reports {
        xml.enabled = true
        html.enabled = false
    }

    def fileFilter = ['**/R.class',
                      '**/R$*.class',
                      '**/BuildConfig.*',
                      '**/Manifest*.*',
                      '**/*Test*.*',
                      'android/**/*.*']
    def debugTree = fileTree(dir: "${project.buildDir}/intermediates/javac/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java/com/se/npe"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = files("${project.buildDir}/jacoco/testDebugUnitTest.exec")
}